buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
        classpath 'com.github.jengelman.gradle.plugins:shadow:+'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'java'
apply plugin: 'com.github.johnrengelman.shadow'

def versionObj = new Version(major: 1, minor: 0, revision: 0)

version = versionObj.toString()
group = "cf.wynntils"
archivesBaseName = "Wynntils"

sourceCompatibility = "1.8"
targetCompatibility = "1.8"
compileJava.options.encoding = 'UTF-8'

minecraft {
    version = "1.11.2-13.20.1.2386"
    runDir = "run"

    mappings = "stable_31"
}

repositories {
    mavenCentral()
    jcenter()
    // JourneyMap API snapshots are here
    maven { url 'https://oss.sonatype.org/content/groups/public/' }
}

configurations {
    embed
    compile.extendsFrom(embed)
}

dependencies {
    embed 'com.jagrosh:DiscordIPC:+'
    embed fileTree(dir: 'lib', include: ['*.jar'])
    embed 'com.fasterxml.jackson.core:jackson-core:+'
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar {
    from configurations.embed.collect { it.isDirectory() ? it : zipTree(it) }
}

reobfJar {
    dependsOn shadowJar
}

shadowJar {
    baseName = archivesBaseName + "-" + version
    classifier = null
    version = null
    configurations = [project.configurations.compile]
    relocate "org.slf4j", "com.expansion.relocate.org.slf4j"
    relocate "org.json", "com.expansion.relocate.org.json"
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.4'
}

class Version {
    String major, minor, revision

    static String getBuild() {
        System.getenv("BUILD_NUMBER") ?: System.getProperty("BUILD_NUMBER") ?: "DEV"
    }

    String toString() {
        "${major}.${minor}.${revision}_$build"
    }
}